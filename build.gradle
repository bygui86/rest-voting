import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.1.0'
    }

}
plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '1.5.6.RELEASE'
}

apply plugin: 'com.bmuschko.docker-remote-api'

repositories {
    jcenter()
    maven {
        url "https://grpcvsrest.s3.amazonaws.com/snapshots"
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'com.google.guava:guava:23.0'

    testCompile 'junit:junit:4.12'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.5.+'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'xmlunit:xmlunit:1.6'
}

idea.module {
    outputDir = file('idea-build')
    testOutputDir = file('idea-test-build')
}

bootRepackage {
    mainClass = 'org.grpcvsrest.restvoting.Application'
}

task buildImage(type: DockerBuildImage) {
    dependsOn bootRepackage
    inputDir = project.projectDir
    tag = 'restvoting'
}


task buildDocker(type: DockerBuildImage) {
    dependsOn bootRepackage
    inputDir = project.projectDir
    tag = 'restvoting:latest'
}

task tagDocker(type: Exec) {
    dependsOn buildDocker
    commandLine 'docker', 'tag', 'restvoting:latest', 'gcr.io/alien-fold-180922/restvoting:latest'
}

task pushDocker(type: Exec) {
    dependsOn tagDocker
    commandLine 'gcloud', 'docker', '--', 'push', 'gcr.io/alien-fold-180922/restvoting:latest'
}

task deploy(type: Exec) {
    commandLine 'kubectl', 'create', '-f', 'k8s/restvoting.yaml'
}

task undeploy(type: Exec) {
    commandLine 'kubectl', 'delete', '-f', 'k8s/restvoting.yaml'
}